plugins {
  id 'com.android.library'
}

android {
  sourceSets {
    main {
      java {
        exclude 'ioio/lib/pc/**'
      }
    }
  }


  namespace 'net.sourceforge.smallbasic.ioio'
  compileSdk 34

  defaultConfig {
    minSdk 19
  }

  buildTypes {
    release {
      ndk {
        debugSymbolLevel = 'FULL'
      }
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  externalNativeBuild {
    ndkBuild {
      path './Android.mk'
    }
  }
}

dependencies {
  implementation 'com.github.ytai.ioio:IOIOLibCore:5.07'
}

configurations.implementation.setCanBeResolved(true)

tasks.register('jarWithDependencies', Jar) {
  def libDir = file("${project.buildDir}/../lib")
  configurations.implementation.files.each { dependencyJar ->
    zipTree(dependencyJar).visit { details ->
      if (!details.directory) {
        copy {
          from details.file
          into "${libDir}/${details.path}"
        }
      }
    }
  }
}

assemble.dependsOn jarWithDependencies
